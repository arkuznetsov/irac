// ----------------------------------------------------------
// This Source Code Form is subject to the terms of the
// Mozilla Public License, v.2.0. If a copy of the MPL
// was not distributed with this file, You can obtain one
// at http://mozilla.org/MPL/2.0/.
// ----------------------------------------------------------
// Codebase: https://github.com/ArKuznetsov/irac/
// ----------------------------------------------------------

Перем ТипыОбъектов;
Перем СвойстваОбъектов;
Перем КомандыОбъектов;

Перем Лог;

#Область ПрограммныйИнтерфейс

// Функция возвращает описания свойств для указанного имени типа объектов
//   
// Параметры:
//   ТипОбъектов        - Строка             - имя типа объектов
//
// Возвращаемое значение:
//   Массив(Структура)                       - описания свойств для типа объектов
//       *Имя                - Строка            - имя свойства объекта
//       *ИмяРАК             - Строка            - имя свойства, как оно возвращается утилитой RAC
//       *ПоУмолчанию        - Произвольный      - значение свойства объекта по умолчанию
//       *Чтение             - Булево            - Истина - значение свойства может быть прочитано;
//       *Добавление         - Булево            - Истина - значение свойства может быть установлено при добавлении;
//       *Изменение          - Булево            - Истина - значение свойства может быть установлено при изменении;
//       *ПараметрКоманды    - Строка            - строка параметра команды, как она будет использована
//                                                 при вызове команды (по умолчанию: "--<ИмяРАК>")
//   
Функция СвойстваОбъекта(Знач ТипОбъектов) Экспорт
	
	Свойства = СвойстваОбъектов.Получить(ВРег(ТипОбъектов));

	Если Свойства = Неопределено Тогда
		ВызватьИсключение СтрШаблон("Не найдено описание свойств для объектов ""%1""", ТипОбъектов);
	КонецЕсли;

	Возврат Свойства;

КонецФункции // СвойстваОбъекта()

// Функция возвращает описания команд для указанного имени типа объектов
//   
// Параметры:
//   ТипОбъектов        - Строка       - имя типа объектов
//
// Возвращаемое значение:
//   Структура                         - описания команд для типа объектов
//       *<Имя команды> - Строка           - Имя команды RAC
//   
Функция КомандыОбъекта(Знач ТипОбъектов) Экспорт
	
	Команды = КомандыОбъектов.Получить(ВРег(ТипОбъектов));

	Если Команды = Неопределено Тогда
		ВызватьИсключение СтрШаблон("Не найдено описание команд для объектов ""%1""", ТипОбъектов);
	КонецЕсли;

	Возврат Команды;

КонецФункции // КомандыОбъекта()

// Функция возвращает описание типа объектов для указанного имени типа объектов
//   
// Параметры:
//   ТипОбъектов        - Строка       - имя типа объектов
//
// Возвращаемое значение:
//   Структура                                 - описание типа объектов
//       *Имя                      - Строка        - имя типа объектов
//       *РежимАдминистрирования   - Строка        - режим утилиты RAC (agent, cluster, infobase и т.п.)
//       *Владелец                 - Струткура     - описание типа объекта, владельца 
//                                                   (например: для типа "Кластер.Администратор"
//                                                   будет содержать описание типа "Кластер")
//   
Функция ТипОбъекта(Знач ТипОбъектов) Экспорт
	
	ОписаниеТипа = ТипыОбъектов.Получить(ВРег(ТипОбъектов));

	Если ОписаниеТипа = Неопределено Тогда
		ВызватьИсключение СтрШаблон("Не найден тип объектов ""%1""", ТипОбъектов);
	КонецЕсли;

	Возврат ОписаниеТипа;

КонецФункции // ТипОбъекта()

#КонецОбласти // ПрограммныйИнтерфейс

#Область ПроцедурыЗаполненияОписаний

// Функция создает структуру описания свойства
//   
// Возвращаемое значение:
//   Структура                              - структура описания свойства типа объектов
//   
Функция ПолучитьСтруктуруОписанияСвойства()

	ОписаниеСвойства = Новый Структура();
	ОписаниеСвойства.Вставить("Имя"            , "");
	ОписаниеСвойства.Вставить("ИмяРАК"         , "");
	ОписаниеСвойства.Вставить("ПараметрКоманды", "");
	ОписаниеСвойства.Вставить("ПоУмолчанию"    , "");
	ОписаниеСвойства.Вставить("Чтение"         , Ложь);
	ОписаниеСвойства.Вставить("Добавление"     , Ложь);
	ОписаниеСвойства.Вставить("Изменение"      , Ложь);
	ОписаниеСвойства.Вставить("ПараметрКоманды", "");

	Возврат ОписаниеСвойства;

КонецФункции // ПолучитьСтруктуруОписанияСвойств()

// Функция заполняет и возвращает описание свойства типа объектов
//   
// Параметры:
//   Имя                - Строка                - имя свойства объекта
//   ИмяРАК             - Строка                - имя свойства, как оно возвращается утилитой RAC
//   ПоУмолчанию        - Произвольный          - значение свойства объекта по умолчанию
//   Использование      - Строка, Структура     - строка содержащая флаги использования, разделенные ","
//                                                (Чтение, Добавление, Изменение)
//                                                если указана структура, то в значении можно переопределить
//                                                имя параметра утилиты RAC
// Возвращаемое значение:
//   Структура                               - описание свойства типа объектов
//       *Имя                - Строка            - имя свойства объекта
//       *ИмяРАК             - Строка            - имя свойства, как оно возвращается утилитой RAC
//       *ПоУмолчанию        - Произвольный      - значение свойства объекта по умолчанию
//       *Чтение             - Булево            - Истина - значение свойства может быть прочитано;
//       *Добавление         - Булево            - Истина - значение свойства может быть установлено при добавлении;
//       *Изменение          - Булево            - Истина - значение свойства может быть установлено при изменении;
//       *ПараметрКоманды    - Строка            - строка параметра команды, как она будет использована
//                                                 при вызове команды (по умолчанию: "--<ИмяРАК>")
//   
Функция ПолучитьОписаниеСвойства(Знач Имя
                               , Знач ИмяРАК
	                           , Знач ПоУмолчанию = ""
	                           , Знач Использование = "Чтение")

	Если ТипЗнч(Использование) = Тип("Строка") Тогда
		Использование = Новый Структура(Использование);
		Для Каждого ТекЭлемент Из Использование Цикл
			Использование[ТекЭлемент.Ключ] = Истина;
		КонецЦикла;
	КонецЕсли;

	Если НЕ ТипЗнч(Использование) = Тип("Структура") Тогда
		Использование = Новый Структура();
	КонецЕсли;

	// Если значение начинается со спец. символа "$", то вычисляем как код
	// используется для получения значений перечислений
	Если ВРег(Лев(ПоУмолчанию, 14)) = ВРег("$Перечисления.") Тогда
		ПоУмолчанию = Перечисления.Значение(Сред(ПоУмолчанию, 15));
	КонецЕсли;

	ОписаниеСвойства = ПолучитьСтруктуруОписанияСвойства();
	ОписаниеСвойства.Имя          = Имя;
	ОписаниеСвойства.ИмяРАК       = ИмяРАК;
	ОписаниеСвойства.ПоУмолчанию  = ПоУмолчанию;
	ОписаниеСвойства.Чтение       = Использование.Свойство("Чтение");
	ОписаниеСвойства.Добавление   = Использование.Свойство("Добавление");
	ОписаниеСвойства.Изменение    = Использование.Свойство("Изменение");

	Если Использование.Свойство("ПараметрКоманды") Тогда
		ОписаниеСвойства.ПараметрКоманды = Использование.ПараметрКоманды;
	Иначе
		Если ОписаниеСвойства.Добавление ИЛИ ОписаниеСвойства.Изменение Тогда
			ОписаниеСвойства.ПараметрКоманды = "--" + ОписаниеСвойства.ИмяРАК;
		КонецЕсли;
	КонецЕсли;

	Возврат ОписаниеСвойства;

КонецФункции // ПолучитьОписаниеСвойства()

// Процедура добавляет описание свойства в массив описаний свойств
//   
// Параметры:
//   ОписаниеСвойствОбъекта  - Массив                - массив описаний свойств для добавления нового описания
//   Имя                     - Строка                - имя свойства объекта
//   СтруктураСвойства       - Структура             - структура с описанием свойства типа объектов
//       *ИмяРАК                 - Строка                - имя свойства, как оно возвращается утилитой RAC
//       *ПоУмолчанию            - Произвольный          - значение свойства объекта по умолчанию
//       *Использование          - Строка, Структура     - строка содержащая флаги использования, разделенные ","
//                                                         (Чтение, Добавление, Изменение)
//                                                         если указана структура, то в значении можно переопределить
//                                                         имя параметра утилиты RAC
//   
Процедура ДобавитьОписаниеСвойстваОбъекта(ОписаниеСвойствОбъекта, Знач Имя, Знач СтруктураСвойства)

	Если НЕ ТипЗнч(ОписаниеСвойствОбъекта) = Тип("Массив") Тогда
		ОписаниеСвойствОбъекта = Новый Массив();
	КонецЕсли;

	Если НЕ СтруктураСвойства.Свойство("ПоУмолчанию") Тогда
		СтруктураСвойства.Вставить("ПоУмолчанию", "");
	КонецЕсли;

	Если НЕ СтруктураСвойства.Свойство("Использование") Тогда
		СтруктураСвойства.Вставить("Использование", "Чтение");
	КонецЕсли;
	
	ОписаниеСвойства = ПолучитьОписаниеСвойства(Имя, 
												СтруктураСвойства.ИмяРАК,
												СтруктураСвойства.ПоУмолчанию,
												СтруктураСвойства.Использование);

	ОписаниеСвойствОбъекта.Добавить(ОписаниеСвойства);

КонецПроцедуры // ДобавитьОписаниеСвойстваОбъекта()

// Процедура добавляет описания свойств указанного типа объектов в соответствие описаний свойств
// для каждого типа объектов будут добавлены записи с ключами:
//    - <ИмяТипа>
//    - <Имя> из описания типа
//    - <РежимАдминистрирования> из описания типа
// если указаны имена родетелей, то для каждого имени родителя и типа объектов будут добавлены записи с ключами:
//    - <имя родителя>.<ИмяТипа>
//    - <имя родителя>.<Имя>
//    - <имя родителя>.<РежимАдминистрирования>
//   
// Параметры:
//   ОписаниеТипаОбъектов        - КлючИЗначение         - Ключ - имя типа объектов, как оно было загружено из макета;
//                                                         Значение - описание типа объектов,
//                                                                    как оно было загружено из макета
//       *Имя                        - Строка                - имя типа объектов
//       *РежимАдминистрирования     - Строка                - имя режима утилиты RAC (agent, cluster, infobase и т.п.)
//       *МинВерсия                  - Строка                - версия 1С, с которой доступен указанный режим RAC
//       *Свойства                   - Структура             - структура описаний свойств типа объектов
//       *Команды                    - Структура             - структура описаний команд типа объектов
//   ИмяТипа                         - Строка            - имя типа, для которого получаем свойства
//   ИменаРодителя                   - Массив(Строка)    - имена родительского типа
//   
Процедура ДобавитьСвойстваТипаОбъектов(Знач ОписаниеТипаОбъектов
	                                 , Знач ИмяТипа
	                                 , Знач ИменаРодителя = Неопределено)

	// Соберем все возможные комбинации имен типов объектов
	ИменаТипа = Новый Массив();
	ИменаТипа.Добавить(ИмяТипа);
	ИменаТипа.Добавить(ОписаниеТипаОбъектов.Имя);
	ИменаТипа.Добавить(ОписаниеТипаОбъектов.РежимАдминистрирования);
	ИменаТипа = ПолучитьВозможныеИменаТипа(ИменаТипа, ИменаРодителя);

	ОписаниеСвойств = Новый Массив();

	Если НЕ ОписаниеТипаОбъектов.Свойство("Свойства") Тогда
		ОписаниеТипаОбъектов.Вставить("Свойства", Новый Структура());
	КонецЕсли;

	Для Каждого ТекСвойство Из ОписаниеТипаОбъектов.Свойства Цикл
		Если ТекСвойство.Значение.Свойство("РежимАдминистрирования") Тогда
			ДобавитьСвойстваТипаОбъектов(ТекСвойство.Значение, ТекСвойство.Ключ, ИменаТипа);
		Иначе
			ДобавитьОписаниеСвойстваОбъекта(ОписаниеСвойств,
											ТекСвойство.Ключ,
											ТекСвойство.Значение);
		КонецЕсли;
	КонецЦикла;

	Для Каждого ТекИмя Из ИменаТипа Цикл
		Если НЕ СвойстваОбъектов.Получить(ВРег(ТекИмя)) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СвойстваОбъектов.Вставить(ВРег(ТекИмя), ОписаниеСвойств);
		Лог.Отладка("Добавлено описание свойств типа объектов: %1", ТекИмя);
	КонецЦикла;

КонецПроцедуры // ДобавитьСвойстваТипаОбъектов()

// Функция создает структуру описания команды
//   
// Возвращаемое значение:
//   Структура                              - структура описания команды типа объектов
//   
Функция ПолучитьСтруктуруОписанияКоманды()

	ОписанияКоманды = Новый Структура();
	ОписанияКоманды.Вставить("Имя"                           , "");
	ОписанияКоманды.Вставить("ИмяРАК"                        , "");
	// добавить параметры авторизации агента кластера
	ОписанияКоманды.Вставить("АвторизацияАгента"             , Ложь);
	// добавить идентификатор кластера и параметры авторизации кластера
	ОписанияКоманды.Вставить("Кластер"                       , Ложь);
	// для подчиненных объектов, добавить текущий (дочерний) режим администрирования
	// (например: process.license - если "Ложь", то "license" добавлено не будет)
	ОписанияКоманды.Вставить("ДочернийРежимАдминистрирования", Истина);
	// добавить общие параметры для родительской команды (режима администрирования)
	// (например в команде: "profile acl --name=<имя профиля> list",
	// часть "acl --name=<имя профиля>" добавлена из общих параметров)
	ОписанияКоманды.Вставить("ОбщиеПараметры"                , Новый Массив());
	// добавить параметры для текущей команды
	ОписанияКоманды.Вставить("ПараметрыКоманды"              , Новый Массив());
	// добавлять значения полей объекта
	// используется для команд добавления и изменения, чтобы задать значения свойств
	ОписанияКоманды.Вставить("ЗначенияПолей"                 , "");

	Возврат ОписанияКоманды;

КонецФункции // ПолучитьСтруктуруОписанияКоманды()

// Процедура добавляет описания команд указанного типа объектов в соответствие описаний команд
// для каждого типа объектов будут добавлены записи с ключами:
//    - <ИмяТипа>
//    - <Имя> из описания типа
//    - <РежимАдминистрирования> из описания типа
// если указаны имена родителей, то для каждого имени родителя и типа объектов будут добавлены записи с ключами:
//    - <имя родителя>.<ИмяТипа>
//    - <имя родителя>.<Имя>
//    - <имя родителя>.<РежимАдминистрирования>
//   
// Параметры:
//   ОписаниеТипаОбъектов        - КлючИЗначение         - Ключ - имя типа объектов, как оно было загружено из макета;
//                                                         Значение - описание типа объектов,
//                                                                    как оно было загружено из макета
//       *Имя                        - Строка                - имя типа объектов
//       *РежимАдминистрирования     - Строка                - имя режима утилиты RAC (agent, cluster, infobase и т.п.)
//       *МинВерсия                  - Строка                - версия 1С, с которой доступен указанный режим RAC
//       *Свойства                   - Структура             - структура описаний свойств типа объектов
//       *Команды                    - Структура             - структура описаний команд типа объектов
//   ИмяТипа                         - Строка            - имя типа, для которого получаем команды
//   ИменаРодителя                   - Массив(Строка)    - имена родительского типа
//   
Процедура ДобавитьКомандыТипаОбъектов(Знач ОписаниеТипаОбъектов
	                                , Знач ИмяТипа
	                                , Знач ИменаРодителя = Неопределено)

	// Соберем все возможные комбинации имен типов объектов
	ИменаТипа = Новый Массив();
	ИменаТипа.Добавить(ИмяТипа);
	ИменаТипа.Добавить(ОписаниеТипаОбъектов.Имя);
	ИменаТипа.Добавить(ОписаниеТипаОбъектов.РежимАдминистрирования);
	ИменаТипа = ПолучитьВозможныеИменаТипа(ИменаТипа, ИменаРодителя);

	Если НЕ ОписаниеТипаОбъектов.Свойство("Свойства") Тогда
		ОписаниеТипаОбъектов.Вставить("Свойства", Новый Структура());
	КонецЕсли;

	Для Каждого ТекСвойство Из ОписаниеТипаОбъектов.Свойства Цикл
		Если ТекСвойство.Значение.Свойство("РежимАдминистрирования") Тогда
			ДобавитьКомандыТипаОбъектов(ТекСвойство.Значение, ТекСвойство.Ключ, ИменаТипа);
		КонецЕсли;
	КонецЦикла;

	ОписаниеКоманд = Новый Структура();

	Если НЕ ОписаниеТипаОбъектов.Свойство("Команды") Тогда
		ОписаниеТипаОбъектов.Вставить("Команды", Новый Структура());
	КонецЕсли;

	Для Каждого ТекКоманда Из ОписаниеТипаОбъектов.Команды Цикл
		ОписаниеКоманды = ПолучитьСтруктуруОписанияКоманды();
		ЗаполнитьЗначенияСвойств(ОписаниеКоманды, ТекКоманда.Значение);
		ОписаниеКоманд.Вставить(ТекКоманда.Ключ, ОписаниеКоманды);
	КонецЦикла;

	Для Каждого ТекИмя Из ИменаТипа Цикл
		Если НЕ КомандыОбъектов.Получить(ВРег(ТекИмя)) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		КомандыОбъектов.Вставить(ВРег(ТекИмя), ОписаниеКоманд);
		Лог.Отладка("Добавлено описание команд типа объектов: %1", ТекИмя);
	КонецЦикла;

КонецПроцедуры // ДобавитьКомандыТипаОбъектов()

// Процедура добавляет описания указанного типа объектов в соответствие описаний типов объектов
// для каждого типа объектов будут добавлены записи с ключами:
//    - <ИмяТипа>
//    - <Имя> из описания типа
//    - <РежимАдминистрирования> из описания типа
// если указаны имена родителей, то для каждого имени родителя и типа объектов будут добавлены записи с ключами:
//    - <имя родителя>.<ИмяТипа>
//    - <имя родителя>.<Имя>
//    - <имя родителя>.<РежимАдминистрирования>
//   
// Параметры:
//   ОписаниеТипаОбъектов        - КлючИЗначение         - Ключ - имя типа объектов, как оно было загружено из макета;
//                                                         Значение - описание типа объектов,
//                                                                    как оно было загружено из макета
//       *Имя                        - Строка                - имя типа объектов
//       *РежимАдминистрирования     - Строка                - имя режима утилиты RAC (agent, cluster, infobase и т.п.)
//       *МинВерсия                  - Строка                - версия 1С, с которой доступен указанный режим RAC
//       *Свойства                   - Структура             - структура описаний свойств типа объектов
//       *Команды                    - Структура             - структура описаний команд типа объектов
//   ИмяТипа                         - Строка            - имя типа, для которого получаем описание
//   ИменаРодителя                   - Массив(Строка)    - имена родительского типа
//   
Процедура ДобавитьОписаниеТипаОбъектов(Знач ОписаниеТипаОбъектов
	                                 , Знач ИмяТипа
	                                 , Знач ИменаРодителя = Неопределено)

	// Соберем все возможные комбинации имен типов объектов
	ИменаТипа = Новый Массив();
	ИменаТипа.Добавить(ОписаниеТипаОбъектов.Имя);
	ИменаТипа.Добавить(ОписаниеТипаОбъектов.РежимАдминистрирования);
	ИменаТипа.Добавить(ИмяТипа);
	ИменаТипа = ПолучитьВозможныеИменаТипа(ИменаТипа, ИменаРодителя);

	Если НЕ ОписаниеТипаОбъектов.Свойство("Свойства") Тогда
		ОписаниеТипаОбъектов.Вставить("Свойства", Новый Структура());
	КонецЕсли;

	ОписаниеТипа = Новый Структура();
	ОписаниеТипа.Вставить("Имя"                   , ИменаТипа[0]);
	ОписаниеТипа.Вставить("ИмяКоллекции"          , ИмяТипа);
	ОписаниеТипа.Вставить("ИмяРАК"                , ОписаниеТипаОбъектов.РежимАдминистрирования);
	ОписаниеТипа.Вставить("РежимАдминистрирования", ОписаниеТипаОбъектов.РежимАдминистрирования);
	Если ТипЗнч(ИменаРодителя) = Тип("Массив") И ИменаРодителя.Количество() > 0 Тогда
		ОписаниеТипа.Вставить("Владелец"          , ТипОбъекта(ИменаРодителя[0]));
	КонецЕсли;

	Для Каждого ТекИмя Из ИменаТипа Цикл
		Если НЕ ТипыОбъектов.Получить(ВРег(ТекИмя)) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ТипыОбъектов.Вставить(ВРег(ТекИмя), ОписаниеТипа);
		Лог.Отладка("Добавлено имя типа объектов %1: %2", ИменаТипа[0], ТекИмя);
	КонецЦикла;

	Для Каждого ТекСвойство Из ОписаниеТипаОбъектов.Свойства Цикл
		Если ТекСвойство.Значение.Свойство("РежимАдминистрирования") Тогда
			ДобавитьОписаниеТипаОбъектов(ТекСвойство.Значение, ТекСвойство.Ключ, ИменаТипа);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры // ДобавитьОписаниеТипаОбъектов()

#КонецОбласти // ПроцедурыЗаполненияОписаний

#Область Служебные

// Функция возвращает массив всех возможныех имена типа с учетом иерархии типов и альтернативных имен
// для каждого имени типа объектов и каждого имени родительского типа будут добавлены имена вида:
//    - <имя родителя>.<ИмяТипа>
// если имена родительских типов не указаны, то будет возвращен массив имен типов без изменений
//
// Параметры:
//   ИменаТипа               - Строка            - имена типа
//   ИменаРодителя           - Массив(Строка)    - имена родительского типа
//   
Функция ПолучитьВозможныеИменаТипа(ИменаТипа, ИменаРодителя = Неопределено)

	Если НЕ (ТипЗнч(ИменаРодителя) = Тип("Массив") И ИменаРодителя.Количество() > 0) Тогда
		Возврат ИменаТипа;
	КонецЕсли;

	ИменаСРодителями = Новый Массив();
	Для Каждого ТекИмяРодителя Из ИменаРодителя Цикл
		Для Каждого ТекИмя Из ИменаТипа Цикл
			ИменаСРодителями.Добавить(СтрШаблон("%1.%2", ТекИмяРодителя, ТекИмя));
		КонецЦикла;
	КонецЦикла;

	Возврат ИменаСРодителями;

КонецФункции // ПолучитьВозможныеИменаТипа

// Процедура заполняет описания типов объектов из макета JSON ("ТипыОбъектовКластера")
// выполняется при инициализации модуля
//
Процедура Инициализация()
	
	Лог = Служебный.Лог();

	ДанныеМакета = Служебный.ПрочитатьДанныеИзМакетаJSON("ТипыОбъектовКластера");

	ТипыОбъектов     = Новый Соответствие();
	СвойстваОбъектов = Новый Соответствие();
	КомандыОбъектов  = Новый Соответствие();

	Для Каждого ТекТип Из ДанныеМакета Цикл

		Если НЕ ТекТип.Значение.Свойство("Свойства") Тогда
			ТекТип.Значение.Вставить("Свойства", Новый Структура());
		КонецЕсли;

		Если НЕ ТекТип.Значение.Свойство("Команды") Тогда
			ТекТип.Значение.Вставить("Команды", Новый Структура());
		КонецЕсли;

		ДобавитьОписаниеТипаОбъектов(ТекТип.Значение, ТекТип.Ключ);

		ДобавитьСвойстваТипаОбъектов(ТекТип.Значение, ТекТип.Ключ);

		ДобавитьКомандыТипаОбъектов(ТекТип.Значение, ТекТип.Ключ);

	КонецЦикла;

КонецПроцедуры // Инициализация()

#КонецОбласти // Служебные

Инициализация();