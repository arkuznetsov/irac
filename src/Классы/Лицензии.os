// ----------------------------------------------------------
// This Source Code Form is subject to the terms of the
// Mozilla Public License, v.2.0. If a copy of the MPL
// was not distributed with this file, You can obtain one
// at http://mozilla.org/MPL/2.0/.
// ----------------------------------------------------------
// Codebase: https://github.com/ArKuznetsov/irac/
// ----------------------------------------------------------

Перем Кластер_Агент;       // - УправлениеКластером1С    - родительский объект агента кластера
Перем Кластер_Владелец;    // - Кластер                  - родительский объект кластера
Перем Владелец;
Перем ИБ_Владелец;

Перем ПараметрыОбъекта;    // - КомандыОбъекта    - объект-генератор команд объекта кластера

Перем Элементы;            // - ОбъектыКластера   - элементы коллекции объектов кластера

Перем Лог;      // - Логирование     - объект-логгер

// Конструктор
//   
// Параметры:
//   АгентКластера     - УправлениеКластером1С    - ссылка на родительский объект агента кластера
//   Кластер           - Кластер                  - ссылка на родительский объект кластера
//   ВладелецЛицензий  - РабочиеПроцессы,         - ссылка на родительский объект рабочих процессов
//                       Сеансы                     или сеансов
//   ИБ                - ИнформационнаяБаза       - ссылка на родительский объект информационной базы
//                                                  используется если владелец "Сеансы"
//
Процедура ПриСозданииОбъекта(АгентКластера, Кластер, ВладелецЛицензий = Неопределено, ИБ = Неопределено)

	Лог = Служебный.Лог();

	Кластер_Агент = АгентКластера;
	Кластер_Владелец = Кластер;
	Владелец = ВладелецЛицензий;
	ИБ_Владелец = ИБ;

	ПараметрыОбъекта = Новый КомандыОбъекта(Кластер_Агент, ТипЛицензий());

	Элементы = Новый ОбъектыКластера(ЭтотОбъект);

КонецПроцедуры // ПриСозданииОбъекта()

// Процедура получает данные по лицензиям от сервиса администрирования кластера 1С
// и сохраняет в локальных переменных
//
// Параметры:
//   РежимОбновления           - Число        - 1 - обновить данные принудительно (вызов RAC)
//                                              0 - обновить данные только по таймеру
//                                             -1 - не обновлять данные
//
Процедура ОбновитьДанные(РежимОбновления = 0) Экспорт
	
	Если НЕ ТребуетсяОбновление(РежимОбновления) Тогда
		Возврат;
	КонецЕсли;

	ПараметрыКоманды = Новый Соответствие();
	ПараметрыКоманды.Вставить("СтрокаПодключенияАгента"     , Кластер_Агент.СтрокаПодключения());
	ПараметрыКоманды.Вставить("ИдентификаторКластера"       , Кластер_Владелец.Ид());
	ПараметрыКоманды.Вставить("ПараметрыАвторизацииКластера", Кластер_Владелец.ПараметрыАвторизации());
	
	Если ТипЗнч(Владелец) = Тип("РабочийПроцесс") Тогда
		ПараметрыКоманды.Вставить("ИдентификаторПроцесса"    , Владелец.Ид());
		ИмяКоманды = "Описание";
		ТекстВладельца = СтрШаблон(" рабочего процесса ""%1:%2""", Владелец.АдресСервера(), Владелец.ПортСервера());
	ИначеЕсли ТипЗнч(Владелец) = Тип("Сеанс") Тогда
		ПараметрыКоманды.Вставить("ИдентификаторСеанса"    , Владелец.Ид());
		ИмяКоманды = "Описание";
		ТекстВладельца = СтрШаблон(" сеанса ""%1""", Владелец.Ид());
	Иначе
		ИмяКоманды = "Список";
		ТекстВладельца = "";
	КонецЕсли;

	Если ТипЛицензий() = Перечисления.РежимыАдминистрирования.ЛицензииСеансов
	   И НЕ ИБ_Владелец = Неопределено Тогда
		ПараметрыКоманды.Вставить("ИдентификаторИБ", ИБ_Владелец.Ид());
	КонецЕсли;

	ПараметрыОбъекта.УстановитьЗначенияПараметровКоманд(ПараметрыКоманды);

	КодВозврата = ПараметрыОбъекта.ВыполнитьКоманду(ИмяКоманды);
	
	Если НЕ КодВозврата = 0 Тогда
		ТекстОшибки = СтрШаблон("Ошибка получения списка лицензий%1 кластера ""%2"", КодВозврата = %3:%4%5",
		                        ТекстВладельца,
		                        Кластер_Владелец.Имя(),
		                        КодВозврата,
		                        Символы.ПС,
		                        Кластер_Агент.ВыводКоманды(Ложь));
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	МассивРезультатов = Кластер_Агент.ВыводКоманды();

	МассивЛицензий = Новый Массив();
	Для Каждого ТекОписание Из МассивРезультатов Цикл
		МассивЛицензий.Добавить(Новый ОбъектКластера(Кластер_Агент, Кластер_Владелец, ТипЛицензий(), ТекОписание));
	КонецЦикла;

	Элементы.Заполнить(МассивЛицензий);

	Элементы.УстановитьАктуальность();

КонецПроцедуры // ОбновитьДанныеЛицензий()

// Функция признак необходимости обновления данных
//   
// Параметры:
//   РежимОбновления           - Число        - 1 - обновить данные принудительно (вызов RAC)
//                                              0 - обновить данные только по таймеру
//                                             -1 - не обновлять данные
//
// Возвращаемое значение:
//    Булево - Истина - требуется обновитьданные
//
Функция ТребуетсяОбновление(РежимОбновления = 0) Экспорт

	Возврат Элементы.ТребуетсяОбновление(РежимОбновления);

КонецФункции // ТребуетсяОбновление()

// Функция возвращает описание параметров объекта
//   
// Возвращаемое значение:
//    КомандыОбъекта - описание параметров объекта,
//
Функция ПараметрыОбъекта() Экспорт

	Возврат ПараметрыОбъекта;

КонецФункции // ПараметрыОбъекта()

// Функция возвращает тип лицензии по типу владельца
//
// Возвращаемое значение:
//    Перечисление.РежимыАдминистрирования - тип лицензии
//
Функция ТипЛицензий() Экспорт

	Если ТипЗнч(Владелец) = Тип("РабочиеПроцессы")
	 ИЛИ ТипЗнч(Владелец) = Тип("РабочийПроцесс") Тогда
		Возврат Перечисления.РежимыАдминистрирования.ЛицензииПроцессов;
	ИначеЕсли ТипЗнч(Владелец) = Тип("Сеансы")
	      ИЛИ ТипЗнч(Владелец) = Тип("Сеанс") Тогда
		Возврат Перечисления.РежимыАдминистрирования.ЛицензииСеансов;
	Иначе
		ВызватьИсключение СтрШаблон("Некорректный тип ""%1"" владельца лицензий,
		                            | ожидались ""РабочиеПроцессы"" или ""Сеансы""!",
		                            Владелец);
	КонецЕсли;

КонецФункции // ТипЛицензий()

// Функция возвращает список сеансов
//   
// Параметры:
//   Отбор                    - Структура    - Структура отбора сеансов (<поле>:<значение>)
//   РежимОбновления          - Число        - 1 - обновить данные принудительно (вызов RAC)
//                                             0 - обновить данные только по таймеру
//                                            -1 - не обновлять данные
//   ЭлементыКакСоответствия  - Булево,      - Истина - элементы результата будут преобразованы в соответствия
//                              Строка         с именами свойств в качестве ключей
//                                             <Имя поля> - элементы результата будут преобразованы в соответствия
//                                             со значением указанного поля в качестве ключей ("Имя"|"ИмяРАК")
//                                             Ложь - (по умолчанию) элементы будут возвращены как есть
//
// Возвращаемое значение:
//    Массив - список сеансов
//
Функция Список(Отбор = Неопределено, РежимОбновления = 0, ЭлементыКакСоответствия = Ложь) Экспорт

	Возврат Элементы.Список(Отбор, РежимОбновления, ЭлементыКакСоответствия);

КонецФункции // Список()

// Функция возвращает список сеансов
//   
// Параметры:
//   ПоляИерархии             - Строка       - Поля для построения иерархии списка сеансов, разделенные ","
//   РежимОбновления           - Число       - 1 - обновить данные принудительно (вызов RAC)
//                                             0 - обновить данные только по таймеру
//                                            -1 - не обновлять данные
//   ЭлементыКакСоответствия  - Булево,      - Истина - элементы результата будут преобразованы в соответствия
//                              Строка         с именами свойств в качестве ключей
//                                             <Имя поля> - элементы результата будут преобразованы в соответствия
//                                             со значением указанного поля в качестве ключей ("Имя"|"ИмяРАК")
//                                             Ложь - (по умолчанию) элементы будут возвращены как есть
//
// Возвращаемое значение:
//    Соответствие - список сеансов
//
Функция ИерархическийСписок(Знач ПоляИерархии, РежимОбновления = 0, ЭлементыКакСоответствия = Ложь) Экспорт

	Возврат Элементы.ИерархическийСписок(ПоляИерархии, РежимОбновления, ЭлементыКакСоответствия);

КонецФункции // ИерархическийСписок()

// Функция возвращает количество сеансов в списке
//   
// Возвращаемое значение:
//    Число - количество сеансов
//
Функция Количество() Экспорт

	Если Элементы = Неопределено Тогда
		Возврат 0;
	КонецЕсли;
	
	Возврат Элементы.Количество();

КонецФункции // Количество()

// Функция возвращает описание сеанса кластера 1С
//   
// Параметры:
//   ИдВладельца             - Строка    - идентификатор сеанса или идентификатор процесса
//   РежимОбновления         - Число        - 1 - обновить данные принудительно (вызов RAC)
//                                            0 - обновить данные только по таймеру
//                                           -1 - не обновлять данные
//   КакСоответствие         - Булево    - Истина - результат будет преобразован в соответствие
//
// Возвращаемое значение:
//    Соответствие - описание сеанса 1С
//
Функция Получить(Знач ИдВладельца, Знач РежимОбновления = 0, КакСоответствие = Ложь) Экспорт

	СеансИлиПроцесс = Владелец.Получить(ИдВладельца);

	Отбор = Новый Соответствие();

	Если ТипЗнч(Владелец) = Тип("РабочиеПроцессы") Тогда
		Отбор.Вставить("process", СеансИлиПроцесс.Ид());
	ИначеЕсли ТипЗнч(Владелец) = Тип("Сеансы") Тогда
		Отбор.Вставить("session", СеансИлиПроцесс.Ид());
	Иначе
		Возврат Неопределено;
	КонецЕсли;

	Лицензии = Элементы.Список(Отбор, РежимОбновления, КакСоответствие);

	Если НЕ ЗначениеЗаполнено(Лицензии) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Лицензии[0];

КонецФункции // Получить()
