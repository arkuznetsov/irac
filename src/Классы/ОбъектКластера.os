// ----------------------------------------------------------
// This Source Code Form is subject to the terms of the
// Mozilla Public License, v.2.0. If a copy of the MPL
// was not distributed with this file, You can obtain one
// at http://mozilla.org/MPL/2.0/.
// ----------------------------------------------------------
// Codebase: https://github.com/ArKuznetsov/irac/
// ----------------------------------------------------------

Перем Объект_Тип;
Перем Объект_Свойства;

Перем Кластер_Агент;       // - УправлениеКластером1С    - родительский объект агента кластера

Перем ПараметрыОбъекта;    // - КомандыОбъекта    - объект-генератор команд объекта кластера

// Конструктор
//
// Параметры:
//   АгентКластера     - УправлениеКластером1С    - ссылка на родительский объект агента кластера
//   Кластер           - Кластер                  - ссылка на родительский объект кластера
//   ТипОбъекта        - Перечисления             - имя типа объекта кластера
//                      .РежимыАдминистрирования
//   ОбъектКластера    - Строка, Соответствие     - идентификатор объекта в кластере 1С или параметры объекта
//
Процедура ПриСозданииОбъекта(АгентКластера, Кластер, ТипОбъекта, ОбъектКластера)

	Если НЕ ЗначениеЗаполнено(ОбъектКластера) Тогда
		Возврат;
	КонецЕсли;

	Объект_Тип = ТипОбъекта;

	Кластер_Агент = АгентКластера;

	ПараметрыОбъекта = Новый КомандыОбъекта(Кластер_Агент, Объект_Тип);
	
	Если ТипЗнч(ОбъектКластера) = Тип("Соответствие") Тогда
		Служебный.ЗаполнитьСвойстваОбъекта(ЭтотОбъект, Объект_Свойства, ОбъектКластера);
	КонецЕсли;

КонецПроцедуры // ПриСозданииОбъекта()

// Функция возвращает описание параметров объекта
//
// Возвращаемое значение:
//    КомандыОбъекта - описание параметров объекта,
//
Функция ПараметрыОбъекта() Экспорт

	Возврат ПараметрыОбъекта;

КонецФункции // ПараметрыОбъекта()

// Функция возвращает идентификатор сервера 1С
//
// Возвращаемое значение:
//    Строка - идентификатор сервера 1С
//
Функция ТипОбъекта() Экспорт

	Возврат Объект_Тип;

КонецФункции // ТипОбъекта()

// Функция возвращает значение параметра объекта кластера 1С
//
// Параметры:
//   ИмяПоля                 - Строка        - Имя параметра кластера
//
// Возвращаемое значение:
//    Произвольный - значение параметра объекта кластера 1С
//
Функция Получить(ИмяПоля) Экспорт
	
	ЗначениеПоля = Неопределено;

	Если НЕ Найти("ТИП, TYPE", ВРег(ИмяПоля)) = 0 Тогда
		Возврат Объект_Тип;
	Иначе
		ЗначениеПоля = Объект_Свойства.Получить(ИмяПоля);
	КонецЕсли;
	
	Если ЗначениеПоля = Неопределено Тогда
	
		ОписаниеПараметра = ПараметрыОбъекта.ОписаниеСвойств("ИмяРАК").Получить(ИмяПоля);

		Если НЕ ОписаниеПараметра = Неопределено Тогда
			ЗначениеПоля = Объект_Свойства.Получить(ОписаниеПараметра["Имя"]);
		КонецЕсли;
	КонецЕсли;

	Возврат ЗначениеПоля;
	    
КонецФункции // Получить()
